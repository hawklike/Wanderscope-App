plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-parcelize'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

def gitVersionCode = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-list', 'HEAD', '--count'
        standardOutput = stdout
    }
    def count = stdout.toString().trim().toInteger()
    println "versionCode: $count"
    return stdout.toString().trim().toInteger()
}

android {
    compileSdk 32

    defaultConfig {
        applicationId "cz.cvut.fit.steuejan.wanderscope"
        minSdk 26
        targetSdk 32
        versionCode gitVersionCode().toInteger()
        versionName "1.0.0-beta"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "env"
    productFlavors {
        staging {
            applicationId "cz.cvut.fit.steuejan.wanderscope.dev"
            dimension "env"
            buildConfigField "String", "BASE_URL", '"https://wanderscope-staging.herokuapp.com/"'
            resValue "string", "app_name", "Wanderscope Dev"
        }

        production {
            applicationId "cz.cvut.fit.steuejan.wanderscope"
            dimension "env"
            buildConfigField "String", "BASE_URL", '"https://wanderscope.herokuapp.com/"'
            resValue "string", "app_name", "Wanderscope"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding true
    }
}

ext {
    koin_version = '3.2.0-beta-1'
    timber_version = '5.0.1'
    firebase_version = '29.3.1'
    retrofit_version = '2.9.0'
    moshi_version = '1.13.0'
    security_crypto_version = '1.1.0-alpha01'
    chucker_version = '3.5.2'
    lifecycle_version = '2.4.1'
    datastore_preferences_version = '1.0.0'
    apache_commons_validator_version = '1.7'
    apache_commons_io_version = '2.11.0'
    joda_time_version = '2.10.14'
    splashscreen_version = '1.0.0-rc01'
    shimmer_version = '0.5.0'
    places_sdk_version = '2.6.0'
    fab_speed_dial_version = '3.3.0'
    maps_sdk_version = '18.0.2'
    maps_ktx_version = '3.2.1'
    jwt_decode_version = '2.0.1'
    lingver_version = '1.3.0'
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    //Jetpack Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    //LiveData Extension
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    //Jetpack Security
    //newer version crashes, see: https://stackoverflow.com/q/63597398/9723204
    //and this https://github.com/google/tink/issues/413
    //noinspection GradleDependency
    implementation "androidx.security:security-crypto:$security_crypto_version"

    //Jetpack Preferences Datastore
    implementation "androidx.datastore:datastore-preferences:$datastore_preferences_version"

    //Koin
    implementation "io.insert-koin:koin-android:$koin_version"
    implementation "io.insert-koin:koin-androidx-navigation:$koin_version"

    //Timber
    implementation "com.jakewharton.timber:timber:$timber_version"

    //Firebase
    implementation platform("com.google.firebase:firebase-bom:$firebase_version")
    //Firebase Analytics
    implementation 'com.google.firebase:firebase-analytics-ktx'
    //Firebase Crashlytics
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"

    //Moshi
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    implementation "com.squareup.moshi:moshi-adapters:$moshi_version"

    //JodaTime
    implementation "joda-time:joda-time:$joda_time_version"

    //Chucker
    debugImplementation "com.github.chuckerteam.chucker:library:$chucker_version"
    releaseImplementation "com.github.chuckerteam.chucker:library-no-op:$chucker_version"

    //Apache Commons Validator
    implementation "commons-validator:commons-validator:$apache_commons_validator_version"

    //Apache Commons IO
    implementation "commons-io:commons-io:$apache_commons_io_version"

    //Splash Screen
    implementation "androidx.core:core-splashscreen:$splashscreen_version"

    //Shimmer
    implementation "com.facebook.shimmer:shimmer:$shimmer_version"

    //Places SDK
    implementation "com.google.android.libraries.places:places:$places_sdk_version"

    //Maps SDK
    implementation "com.google.android.gms:play-services-maps:$maps_sdk_version"
    implementation "com.google.maps.android:maps-ktx:$maps_ktx_version"

    //FAB Speed Dial
    implementation "com.leinardi.android:speed-dial:$fab_speed_dial_version"

    //JWT Decode
    implementation "com.auth0.android:jwtdecode:$jwt_decode_version"

    //Lingver
    implementation "com.github.YarikSOffice:lingver:$lingver_version"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}